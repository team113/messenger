plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def properties = new Properties()
def propertiesFile = rootProject.file('key.properties')
def hasProperties = propertiesFile.exists()

android {
    namespace 'com.tapopa.messenger'

    ndkVersion = "28.2.13676358"
    compileSdk = 36

    compileOptions {
        // TODO: Remove when MaikuB/flutter_local_notifications#2389 is fixed:
        //       https://github.com/MaikuB/flutter_local_notifications/issues/2389
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_21
    }

    kotlin {
        jvmToolchain(21)
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.tapopa.messenger"
        minSdkVersion 24
        targetSdkVersion 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        if (hasProperties) {
            release {
                    propertiesFile.withInputStream { properties.load(it) }
                    storeFile file("$properties.storeFile")
                    storePassword "$properties.storePassword"
                    keyAlias "$properties.keyAlias"
                    keyPassword "$properties.keyPassword"
            }
        }
    }

    buildTypes {
        release {
            if (hasProperties) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
    }
    namespace 'com.tapopa.messenger'
}

flutter {
    source '../..'
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:33.5.0')

    // TODO: Remove when MaikuB/flutter_local_notifications#2389 is fixed:
    //       https://github.com/MaikuB/flutter_local_notifications/issues/2389
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
}
